load("//bazel-rules:gen_test.bzl", "gen_test")

package(
    default_visibility = ["//visibility:public"],
)

template_files = glob([
    "modules/**/*",
    "platforms/**/*",
])

config_setting(
    name = "darwin",
    values = {"cpu": "darwin"},
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linux",
    values = {"cpu": "k8"}, # don't ask...
    visibility = ["//visibility:public"],
)

genrule(
    name = "terraform_runtime",
    output_to_bindir = 1,
    srcs = select({
        "//:linux": ["@terraform_runtime_linux//:terraform"],
        "//:darwin": ["@terraform_runtime_darwin//:terraform"],
    }),
    outs = ["bin/terraform"],
    cmd = "cp $(<) $(@)",
    executable = True,
)

genrule(
    name = "provider_matchbox",
    output_to_bindir = 1,
    srcs =  select({
        "//:linux": ["@terraform_provider_matchbox_linux//:terraform-provider-matchbox"],
        "//:darwin": ["@terraform_provider_matchbox_darwin//:terraform-provider-matchbox"],
    }),
    outs = ["bin/terraform-provider-matchbox"],
    cmd = "cp $(<) $(@)",
)

PLATFORMS = ["aws", "azure", "gcp", "govcloud", "metal", "openstack/neutron", "vmware"]

[genrule(
    name = "examples_" + platform,
    message = "Compiling examples for " + platform + "...",
    srcs = ["platforms/" + platform + "/variables.tf", "config.tf"],
    outs = ["examples/terraform.tfvars." + platform],
    cmd = "$(location //contrib/terraform-examples:terraform-examples) $(location :config.tf) $(location :platforms/" + platform + "/variables.tf) > $@",
    tools = ["//contrib/terraform-examples:terraform-examples"],
) for platform in PLATFORMS]

# DOCS is a dict of tuples matching ["name": ("src", "out"), ...].
DOCS = {p: ("platforms/" + p + "/variables.tf", "Documentation/variables/" + p + ".md") for p in PLATFORMS}
DOCS["config"] = ("config.tf", "Documentation/variables/config.md")

[genrule(
    name = "docs_" + doc[0],
    message = "Compiling docs for " + doc[0] + "...",
    srcs = [doc[1][0]],
    outs = [doc[1][1]],
    cmd = "echo '<!-- DO NOT EDIT. THIS FILE IS AUTOMATICALLY GENERATED. -->' > $@ && echo '# Terraform variables' >> $@ && $(location @com_github_segmentio_terraform-docs//:terraform-docs) --no-required markdown $(location :" + doc[1][0] + ") >> $@",
    tools = ["@com_github_segmentio_terraform-docs//:terraform-docs"],
) for doc in DOCS.items()]

genrule(
    name = "templates",
    message = "Copying templates...",
    output_to_bindir = 1,
    srcs = template_files,
    outs = ["templates/%s" % f for f in template_files],
    cmd = '\n'.join([
        "for tf_file in $(SRCS); do",
        "target=\"$(@D)/templates/$$(dirname $${tf_file})\"",
        "mkdir -p $${target}",
        "cp $${tf_file} $${target}",
        "done"
    ]),
)

load("@io_bazel_rules_go//go:def.bzl", "go_prefix")

go_prefix("github.com/coreos/tectonic-installer")

load("@bazel_gazelle//:def.bzl", "gazelle")

gazelle(
    name = "gazelle",
    command = "fix",
    prefix = "github.com/coreos/tectonic-installer",
    external = "vendored",
)

alias(
    name = "smoke_tests",
    actual = "//tests/smoke:smoke",
)

alias(
    name = "backend",
    actual = "//installer/cmd/installer:installer",
)

load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")

# We use a genrule here to combine the tarballs rather than a pkg_tar
# because we want to be able to optionally add a version string to the
# directory name and pkg_tar does not allow for formatted names.
genrule(
    name = "tarball",
    srcs = [
        # Cross compiling to darwin using goos is currently broken.
        # Instead we can cross compile ad hoc on the terminal
        # by explicitly setting the toolchain: https://github.com/bazelbuild/rules_go/issues/1148
        #":tarball_installer_binary_darwin",
        ":tarball_installer_binary_linux",
        ":tarball_terraform_configuration",
        ":tarball_terraform_binary_darwin",
        ":tarball_terraform_binary_linux",
        ":tarball_terraform_provider_matchbox_darwin",
        ":tarball_terraform_provider_matchbox_linux",
    ],
    outs = ["tectonic.tar.gz"],
    cmd = '\n'.join([
            "VERSION=$${VERSION:-}",
            "DIR=tectonic",
            "[ ! -z $$VERSION ] && DIR=\"$$DIR\"_\"$$VERSION\"",
            "for s in $(SRCS); do",
                "tar --extract --file $$s --transform s,^,./$$DIR/,",
            "done",
            "tar --create --gzip --file $@ $$DIR"
    ]),
    output_to_bindir = 1,
)

pkg_tar(
    name = "tarball_terraform_configuration",
    mode = "0644",
    srcs = ["config.tf", "modules", "platforms"],
)

pkg_tar(
    name = "tarball_installer_binary_darwin",
    mode = "0755",
    package_dir = "tectonic-installer/darwin",
    srcs = ["//installer/cmd/installer:installer_darwin"],
)

pkg_tar(
    name = "tarball_installer_binary_linux",
    mode = "0755",
    package_dir = "tectonic-installer/linux",
    srcs = ["//installer/cmd/installer:installer_linux"],
)

pkg_tar(
    name = "tarball_terraform_binary_darwin",
    mode = "0755",
    package_dir = "tectonic-installer/darwin",
    srcs = ["@terraform_runtime_darwin//:terraform"],
)

pkg_tar(
    name = "tarball_terraform_binary_linux",
    mode = "0755",
    package_dir = "tectonic-installer/linux",
    srcs = ["@terraform_runtime_linux//:terraform"],
)

pkg_tar(
    name = "tarball_terraform_provider_matchbox_darwin",
    mode = "0755",
    package_dir = "tectonic-installer/darwin",
    srcs = ["@terraform_provider_matchbox_darwin//:terraform-provider-matchbox"],
)

pkg_tar(
    name = "tarball_terraform_provider_matchbox_linux",
    mode = "0755",
    package_dir = "tectonic-installer/linux",
    srcs = ["@terraform_provider_matchbox_linux//:terraform-provider-matchbox"],
)

filegroup(
    name = "terraform_files",
    srcs = glob(
        ["**/*.tf"],
        exclude=["bazel-bin/**/*", "bazel-out/**/*", "bazel-genfiles/**/*", "build/**/*"],
        exclude_directories=1,
    ),
)

gen_test(
    name = "terraform_fmt",
    command = "./bin/terraform fmt -list -check -write=false",
    deps = [":terraform_runtime", ":terraform_files"],
)
